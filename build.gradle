apply plugin: 'groovy'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'java-gradle-plugin'

description = 'A Gradle plugin that detects the OS name and architecture, providing a uniform\
 classifier to be used in the names of native artifacts.'
group = 'com.google.gradle'

// The major and minor versions are aligned with the Maven plugin's.
version = '1.7.1-SNAPSHOT'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
ext.javaVersion = JavaVersion.current()
ext.pluginId = 'com.google.osdetector'

dependencies {
  compile gradleApi(), localGroovy()
  compile('kr.motd.maven:os-maven-plugin:1.7.0') {
    exclude group: 'org.apache.maven', module: 'maven-plugin-api'
    exclude group: 'org.codehaus.plexus', module: 'plexus-utils'
  }
  testCompile 'junit:junit:4.12'
}

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn:groovydoc) {
  classifier = 'groovydoc'
  from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

tasks.javadoc.options {
  addBooleanOption('Xdoclint:-missing').value = true
}

artifacts {
  archives sourcesJar,
           groovydocJar,
           javadocJar
}

// The Gradle plugin portal doesn't allow signature files.
if (!gradle.startParameter.taskNames.intersect(['publishPlugins'])) {
  signing {
    required { isReleaseVersion }
    sign configurations.archives
  }
}

targetCompatibility = 1.8
sourceCompatibility = 1.8
// Releases must be built on Java 1.8
task checkJavaVersion {
  doLast {
    if (!javaVersion.java8Compatible) {
      throw new GradleException(
          "The plugin must be published under Java 1.8 but ${javaVersion} is found")
    }
  }
}

if (rootProject.hasProperty("ossrhUsername") && rootProject.hasProperty("ossrhPassword")) {
  uploadArchives.dependsOn checkJavaVersion

  uploadArchives.repositories.mavenDeployer {
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: ossrhUsername, password: ossrhPassword)
    }

    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
      authentication(userName: ossrhUsername, password: ossrhPassword)
    }
  }

  [
    install.repositories.mavenInstaller,
    uploadArchives.repositories.mavenDeployer,
  ]*.pom*.whenConfigured { pom ->
    pom.project {
      name "$project.group:$project.name"
      description project.description
      url 'https://github.com/google/osdetector-gradle-plugin'

      scm {
        connection 'scm:svn:https://github.com/google/osdetector-gradle-plugin.git'
        developerConnection 'scm:svn:git@github.com:google/osdetector-gradle-plugin.git'
        url 'https://github.com/google/osdetector-gradle-plugin'
      }

      licenses {
        license {
          name 'Apache License 2.0'
          url 'http://opensource.org/licenses/Apache-2.0'
        }
      }

      developers {
        developer {
          name 'Kun Zhang'
          email 'zhangkun@google.com'
          url 'https://github.com/google/osdetector-gradle-plugin'
          organization = 'Google, Inc.'
          organizationUrl 'https://www.google.com'
        }
      }
    }
  }
}

gradlePlugin {
  plugins {
    osDetectorPlugin {
      id = pluginId
      implementationClass = 'com.google.gradle.osdetector.OsDetectorPlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/google/osdetector-gradle-plugin'
  vcsUrl = 'https://github.com/google/osdetector-gradle-plugin'
  description = 'A Gradle plugin that detects the OS name and architecture, ' +
      'providing a uniform classifier to be used in the names of native artifacts.'

  plugins {
    osDetectorPlugin {
      id = pluginId
      displayName = 'OS name and architecture detector'
      tags = ['os', 'osdetector', 'arch', 'classifier']
    }
  }
}

test {
  testLogging.showStandardStreams = true
}
